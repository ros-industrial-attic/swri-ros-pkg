-- Software License Agreement (BSD License)
--
-- Copyright (c) 2013, Southwest Research Institute
-- All rights reserved.
--
-- Redistribution and use in source and binary forms, with or without
-- modification, are permitted provided that the following conditions
-- are met:
--
--  * Redistributions of source code must retain the above copyright
--    notice, this list of conditions and the following disclaimer.
--  * Redistributions in binary form must reproduce the above
--    copyright notice, this list of conditions and the following
--    disclaimer in the documentation and/or other materials provided
--    with the distribution.
--  * Neither the name of the Southwest Research Institute nor the names 
--    of its contributors may be used to endorse or promote products 
--    derived from this software without specific prior written 
--    permission.
--
-- THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
-- "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
-- LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS
-- FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE
-- COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT,
-- INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
-- BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
-- LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
-- CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
-- LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN
-- ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
-- POSSIBILITY OF SUCH DAMAGE.


PROGRAM libind_rs
--------------------------------------------------------------------------------
-- 
-- Library routines for creating / processing ROS Industrial robot status packets
-- 
-- 
-- author: Shaun Edwards
-- 
--------------------------------------------------------------------------------
%NOLOCKGROUP
%NOPAUSE= COMMAND + TPENABLE + ERROR
%COMMENT = 'ROS Ind Robot Status Packets'




--------------------------------------------------------------------------------
-- 
-- local types & constants
-- 
--------------------------------------------------------------------------------
%INCLUDE libind_rs_t
%INCLUDE libind_ts_t
%INCLUDE libind_pkt_t




--------------------------------------------------------------------------------
-- 
-- remote routine prototypes
-- 
--------------------------------------------------------------------------------




--------------------------------------------------------------------------------
-- 
-- local routine prototypes
-- 
--------------------------------------------------------------------------------
%INCLUDE libind_rs_h
%INCLUDE libind_hdr_h




--------------------------------------------------------------------------------
-- 
-- Empty body
-- 
--------------------------------------------------------------------------------
BEGIN
END libind_rs




ROUTINE irs_ctor
BEGIN
	irs_reset(this)
END irs_ctor




ROUTINE irs_reset
BEGIN
	USING this DO

		ihdr_reset(header_)
		header_.msg_type_ = RI_MT_STATUS

		mode_             = RS_M_UNKNWN
		e_stopped_        = TS_UNKNOWN
		drv_pwd_          = TS_UNKNOWN
		mtn_psble_        = TS_UNKNOWN
		in_motion_        = TS_UNKNOWN
		in_error_         = TS_UNKNOWN
		error_code_       = TS_UNKNOWN
	ENDUSING
END irs_reset




ROUTINE irs_in_mtn
VAR
	len__ : INTEGER
	d__   : INTEGER
BEGIN
	-- 
	len__ = ARRAY_LEN($MOR_GRP[1].$IN_POSITION)
	d__   = 0

	-- Check for not in position (indicates motion)
	FOR d__ = 1 TO len__ DO
		IF (NOT $MOR_GRP[1].$IN_POSITION[d__]) THEN
			RETURN (TRUE)
		ENDIF
	ENDFOR

	-- 
	RETURN (FALSE)
END irs_in_mtn




ROUTINE irs_update
BEGIN
	-- 
	irs_reset(this)

	-- (VERIFIED) teach pendant enabled (see table 15-2, KAREL Reference Manual)
	IF (OPOUT[7]) THEN
		this.mode_ = RS_M_MAN
	ELSE
		this.mode_ = RS_M_AUTO
	ENDIF

	-- e-stop
	this.e_stopped_ = TS_UNKNOWN

	-- (VERIFIED) in motion
	IF (irs_in_mtn) THEN
		this.in_motion_ = TS_TRUE
	ELSE
		this.in_motion_ = TS_FALSE
	ENDIF

	-- POTENTIALLY CAUSES ISSUE ON CONTROLLERS WITHOUT EXTENED IO BOARD
	-- (OPIN/OUT > 16 MAY NOT BE PRESENT
	-- drive power (see table 15-4, KAREL Reference Manual)
	IF (OPOUT[17]) THEN
		this.drv_pwd_ = TS_TRUE
	ENDIF

	-- motion enable (see table 15-3, KAREL Reference Manual)
	IF (OPIN[23]) THEN
		this.mtn_psble_ = TS_TRUE
	ENDIF

	-- fault indicator (see table 15-4, KAREL Reference Manual)
	IF (OPOUT[21]) THEN
		this.in_error_  = TS_TRUE
	ENDIF
END irs_update




ROUTINE irs_tpc_srl
VAR
	stat__ : INTEGER
BEGIN
	--
	stat__ = 0

	this.header_.length_     = RS_SZ_TOPIC
	this.header_.msg_type_   = RI_MT_STATUS
	this.header_.comm_type_  = RI_CT_TOPIC
	this.header_.reply_type_ = RI_RT_INVAL

	stat__ = ihdr_srlise(this.header_, fd)
	IF (stat__ <> 0) THEN RETURN (-ABS(stat__)); ENDIF

	-- write out all fields ..
	WRITE fd(this.drv_pwd_, 
		this.e_stopped_, 
		this.error_code_,
		this.in_error_,
		this.in_motion_,
		this.mode_,
		this.mtn_psble_, 
	CR)

	stat__ = IO_STATUS(fd)

	-- 
	RETURN (-stat__)
END irs_tpc_srl




ROUTINE irs_tostr
VAR
	md_str__  : STRING[20]
	es_str__  : STRING[20]
	dp_str__  : STRING[20]
	mp_str__  : STRING[20]
	im_str__  : STRING[20]
	ie_str__  : STRING[20]
	ec_str__  : STRING[20]
BEGIN
	-- TODO: check for 'uninitialised' data and handle 
	--       those (add 'unknown' or something)
	CNV_INT_STR(this.mode_            , 1, 10, md_str__)
	CNV_INT_STR(this.e_stopped_       , 1, 10, es_str__)
	CNV_INT_STR(this.drv_pwd_         , 1, 10, dp_str__)
	CNV_INT_STR(this.mtn_psble_       , 1, 10, mp_str__)
	CNV_INT_STR(this.in_motion_       , 1, 10, im_str__)
	CNV_INT_STR(this.in_error_        , 1, 10, ie_str__)
	CNV_INT_STR(this.error_code_      , 1, 10, ec_str__)

	-- done
	RETURN ('rs(dsceimp):' + dp_str__  + '' + es_str__ + '' &
		+ ec_str__  + '' + ie_str__ + '' +  im_str__ + '' + '' &
		+ md_str__ + '' +  mp_str__)
END irs_tostr




ROUTINE irs_dump
BEGIN
	WRITE fd(ihdr_tostr(this.header_), CR)
	WRITE fd(irs_tostr(this), CR)
END irs_dump
